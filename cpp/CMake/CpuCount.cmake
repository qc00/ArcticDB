if(NOT DEFINED ENV{CMAKE_BUILD_PARALLEL_LEVEL} OR "$ENV{CMAKE_BUILD_PARALLEL_LEVEL}" STREQUAL "")
    include(ProcessorCount)
    ProcessorCount(_proc_count)
    if (DEFINED ENV{ARCTICDB_REDUCE_CORE_COUNT_TO_MATCH_FREE_RAM})
        cmake_host_system_information(RESULT _physical_mb QUERY AVAILABLE_PHYSICAL_MEMORY)
        math(EXPR _by_ram "${_physical_mb} / 1500")
        if (${_by_ram} LESS ${_proc_count})
            set(_proc_count ${_by_ram})
        endif()
        if (${_proc_count} LESS 1)
            set(_proc_count 1)
        endif()
    elseif(${_proc_count} LESS 2)
        message(WARNING "Failed to determine the available processor count. Assuming you have at least 2 cores. \
            Please set the CMAKE_BUILD_PARALLEL_LEVEL environment variable.")
        set(_proc_count 2)
    else()
        cmake_host_system_information(RESULT _physical_mb QUERY TOTAL_PHYSICAL_MEMORY)
        math(EXPR _needed_mb "${_proc_count} * 1500")
        if(${_needed_mb} GREATER ${_physical_mb})
            message(WARNING "We recommend 1500MB of physical RAM per processor core. We found ${_proc_count} cores and\
                ${_physical_mb}MB of RAM. Use the CMAKE_BUILD_PARALLEL_LEVEL environment variable to avoid thrashing.")
        endif()
    endif()
    set(ENV{CMAKE_BUILD_PARALLEL_LEVEL} ${_proc_count})
endif()
message(STATUS "CMAKE_BUILD_PARALLEL_LEVEL=$ENV{CMAKE_BUILD_PARALLEL_LEVEL}")
